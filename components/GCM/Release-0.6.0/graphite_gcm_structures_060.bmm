-- 
-- 	component:   Graphite Canonical Model BMM
-- 	description: Graphite Canonical Model component formal expression. This file is an ODIN serialisation of
--                   the BMM object meta-model classes found at 
--                   https://www.openEHR.org/releases/LANG/latest/p_bmm.html
-- 	keywords:    reference model, meta-model, archetypes
-- 	author:      Thomas Beale <thomas.beale@graphitehealth.io>
-- 	support:     https://graphite.atlassian.net/issues/
-- 	copyright:   Copyright (c) 2023- Graphite Health <https://www.graphitehealth.io>
-- 	license:     Apache 2.0 <http://www.apache.org/licenses/LICENSE-2.0.html>
--

------------------------------------------------------
-- BMM version on which these schemas are based.
-- Current BMM version can be found as the top version in the specification:
--     https://www.graphitehealth.io/releases/LANG/latest/bmm_persistence.html#_amendment_record
--
------------------------------------------------------
bmm_version = <"2.3">

------------------------------------------------------
-- schema identification
-- (schema_id computed as <rm_publisher>_<schema_name>_<rm_release>)
------------------------------------------------------
schema_name = <"gcm_structures">
rm_publisher = <"graphite">
rm_release = <"0.6.0">

------------------------------------------------------
-- schema documentation
------------------------------------------------------
schema_revision = <"0.6.0.1">
schema_lifecycle_state = <"stable">
schema_description = <"openEHR Release 0.6.0 structures schema">
schema_author = <"Thomas Beale <thomas.beale@graphitehealth.io>">

------------------------------------------------------
-- inclusions
------------------------------------------------------
includes = <
	["1"] = <
		id = <"graphite_gcm_data_types_0.6.0">
	>
>

------------------------------------------------------
-- packages
------------------------------------------------------

packages = <
	["org.graphite.gcm.data_structures"] = <
		name = <"org.graphite.gcm.data_structures">
		classes = <"Item", "Cluster", "Element", "History", "Event", "Point_event", "Interval_event">
	>
>


------------------------------------------------------
-- classes
------------------------------------------------------

class_definitions = <

	------------------------------------------------------------
	---------------------- gcm.data_structures ------------------
	------------------------------------------------------------

	["Item"] = <
		name = <"Item">
		ancestors = <"Locatable">
		is_abstract = <True>
	>

	["Cluster"] = <
		name = <"Cluster">
		ancestors = <"Item">
		properties = <
			["items"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"items">
				type_def = <
					container_type = <"List">
					type = <"Item">
				>
				cardinality = <|>=1|>
				is_mandatory = <True>
			>
		>
	>

	["Element"] = <
		name = <"Element">
		ancestors = <"Item">
		properties = <
			["null_flavour"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"null_flavour">
				type = <"Terminology_term">
			>
			["value"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"value">
				type = <"Data_value">
			>
			["null_reason"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"null_reason">
				type = <"DV_TEXT">
			>
		>
	>

	["History"] = <
		name = <"History">
		ancestors = <"Locatable">
		generic_parameter_defs = <
			["T"] = <
				name = <"T">
				conforms_to_type = <"Cluster">
			>
		>
		properties = <
			["origin"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"origin">
				type = <"Date_time">
				is_mandatory = <True>
				is_im_runtime = <True>
			>
			["period"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"period">
				type = <"Duration">
			>
			["duration"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"duration">
				type = <"Duration">
			>
			["summary"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"summary">
				type = <"Cluster">
			>
			["events"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"events">
				type_def = <
					container_type = <"List">
					type_def = (P_BMM_GENERIC_TYPE) <
						root_type = <"Event">
						generic_parameters = <"T">
					>
				>
				cardinality = <|>=1|>
			>
		>
	>

	["Event"] = <
		name = <"Event">
		ancestors = <"Locatable">
		is_abstract = <True>
		generic_parameter_defs = <
			["T"] = <
				name = <"T">
				conforms_to_type = <"Cluster">
			>
		>
		properties = <
			["time"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"time">
				type = <"Date_time">
				is_mandatory = <True>
				is_im_runtime = <True>
			>
			["state"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"state">
				type = <"Cluster">
			>
			["data"] = (P_BMM_SINGLE_PROPERTY_OPEN) <
				name = <"data">
				type = <"T">
				is_mandatory = <True>
			>
			["offset"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"offset">
				is_computed = <True>
				type = <"Duration">
			>
		>
	>

	["Point_event"] = <
		name = <"Point_event">
		ancestors = <"Event">
		generic_parameter_defs = <
			["T"] = <
				name = <"T">
			>
		>
	>

	["Interval_event"] = <
		name = <"Interval_event">
		ancestors = <"Event">
		generic_parameter_defs = <
			["T"] = <
				name = <"T">
			>
		>
		properties = <
			["width"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"width">
				type = <"Duration">
				is_mandatory = <True>
			>
			["sample_count"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"sample_count">
				type = <"Integer">
				is_im_runtime = <True>
			>
			["math_function"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"math_function">
				type = <"Terminology_term">
				is_mandatory = <True>
			>
		>
	>

>
